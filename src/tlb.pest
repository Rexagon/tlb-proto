WHITESPACE = _{ " " | NEWLINE }
COMMENT = _{
    ("//" ~ (!NEWLINE ~ ANY)*) |
    ("/*" ~ (!"*/" ~ ANY)* ~ "*/")
}

tlb_scheme = _{ SOI ~ (constructor ~ ";")* ~ EOI }
tlb_constructor = _{ SOI ~ constructor ~ ";"? ~ EOI }

constructor = { constructor_name ~ type_arg* ~ field_group_item* ~ "=" ~ output_type }

// Constructor name
constructor_name = { ("_" | lc_ident) ~ constructor_id? }
constructor_id = _{
	("$" ~ (constructor_id_empty | constructor_id_binary)) |
    ("#" ~ (constructor_id_empty | constructor_id_hex))
}
constructor_id_binary = @{ ident_char+ }
constructor_id_hex = @{ ident_char+ }
constructor_id_empty = @{ "_" }

// Named type argument
type_arg = { "{" ~ ident ~ ":" ~ type_expr ~ "}" }

// Multiple fields
field_group_item = {
    (("_" ~ ":")? ~ "^[" ~ field_group_item+ ~ "]") |
    field |
    guard_expr
}
// Field with name or unnamed
field = { field_named | type_expr }
// Field with name
field_named = { field_ident ~ ":" ~ conditional_def? ~ type_expr }
// Optional field which is set only if the specified bit is set
conditional_def = { ident ~ "." ~ nat_const ~ "?" }

// Type expression
type_expr = {
    nat_const |
    nat_type |
    ident |
    type_in_cell |
    ( "(" ~ (nat_type_eq_bits | nat_type_le_bits | nat_type_lt_bits) ~ ")" ) |
    ( "(" ~ type_nat_expr ~ ")" ) |
    ( "(" ~ ident ~ type_expr* ~ ")" )
}
// Put type to the child cell
type_in_cell = { "^" ~ type_expr }
type_nat_expr = { ident ~ nat_operator ~ (nat_const | ident) }

// Possibly anonymous field ident
field_ident = @{ "_" | ident }

// Guard expression
guard_expr = {
    "{" ~ ident ~ guard_operator ~ (nat_const | ident) ~ "}"
}
// Comparison operator
guard_operator = @{ "<=" | "<" | "=" | ">=" | ">" }

// Output type with type parameters substitution
output_type = { uc_ident ~ type_expr* }

// [a-zA-Z0-9_]
ident_char = @{ ASCII_ALPHANUMERIC | "_" }

// Operator for idents with nat consts
nat_operator = @{ "+" | "-" | "*" | "/" }

// Alias to uint32
nat_type = @{ "#" }
// N-bit integer
nat_type_eq_bits = { "##" ~ (nat_const | ident) }
// Variable integer with at most N bits
nat_type_le_bits = { "#<=" ~ (nat_const | ident) }
// Variable integer with less than N bits
nat_type_lt_bits = { "#<" ~ (nat_const | ident) }

// Field or type ident
ident = @{ lc_ident | uc_ident }
// Ident starting from the lowercase letter
lc_ident = @{ ASCII_ALPHA_LOWER ~ ident_char* }
// Ident starting from the uppercase letter
uc_ident = @{ ASCII_ALPHA_UPPER ~ ident_char* }

// Constants
nat_const = @{ ASCII_DIGIT+ }
